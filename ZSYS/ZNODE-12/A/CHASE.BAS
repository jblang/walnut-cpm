10 REM        C H A S E       V 1.0        1/16/77   GCC III
20 REM BY HERMAN DEMONSTOY FEB 77 ISSUE KILOBAUD MAG
30 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
40 Z2=0
50 PRINT"THIS IS THE GAME OF CHASE"
60 PRINT"YOU ARE IN A HIGH VOLTAGE MAZE WITH FIVE"
70 PRINT"SECURITY MACHINES (+) TRYING TO DESTROY YOU (X)."
80 PRINT"YOU MUST MANEUVER THE SECURITY MACHINES INTO"
90 PRINT"THE HIGH VOLTAGE FENCE POSTS (O), OR INTO EACH OTHER"
100 PRINT"IN ORDER TO SURVIVE.   GOOD LUCK.
110 PRINT
120 PRINT"MOVES ARE:    1 2 3"
130 PRINT"              8 * 4     0  IS NO MOVE"
140 PRINT"              7 6 5     9  GIVE UP"
150 PRINT
160 REM .............SET UP THE GAME
170 DIM A(10,20),E(21),F(21)
180 G=5:M=0
190 FOR B=1 TO 10
200 FOR C=1 TO 20
210 LET A(B,C)=0
220 IF B=1 THEN A(B,C)=1
230 IF B=10 THEN A(B,C)=1
240 IF C=1 THEN A(B,C)=1
250 IF C=20 THEN A(B,C)=1
260 NEXT C
270 PRINT" . ";:REM ..........KEEPS SCREEN GOING WHILE TABLE SET UP
280 NEXT B
290 FOR D=1 TO 21
300 B=INT(RND(1)*8)+2
310 C=INT(RND(1)*18)+2:REM .......................
320 IF A(B,C)<>0 GOTO 300
330 A(B,C)=1
340 IF D<6 THEN A(B,C)=2
350 IF D=6 THEN A(B,C)=3
360 E(D)=B
370 F(D)=C
380 NEXT D
390 PRINT:PRINT
400 REM ..........PRINT PATTERN
410 FOR B=1 TO 10
420 FOR C=1 TO 20
430 IF A(B,C)=0 THEN  PRINT" ";
440 IF A(B,C)=1 THEN PRINT"O";
450 IF A(B,C)=2 THEN PRINT"+";
460 IF A(B,C)=3 THEN PRINT"X";
470 NEXT C
480 IF B=3 THEN PRINT"      ";G;"SECURITY MACHINE";:IF G>1 THEN PRINT"S";
490 IF B=3 THEN PRINT" LEFT";
500 IF B=7 THEN PRINT"            1 2 3";
510 IF B=8 THEN PRINT"            8 * 4     0  IS NO MOVE";
520 IF B=9 THEN PRINT"            7 6 5     9  GIVE UP";
530 PRINT
540 NEXT B
550 REM .........MAKE MOVE
560 B=E(6)
570 C=F(6)
580 A(B,C)=0
590 INPUT"MOVE";Y
600 M=M+1
610 IF Y=0 GOTO 710
620 ON Y GOTO 630,630,630,660,650,650,650,660,890
630 B=B-1
640 GOTO 660
650 B=B+1
660 ON Y GOTO 670,710,690,690,690,710,670,670
670 C=C-1
680 GOTO 710
690 C=C+1
700 REM ..........CALCULATE THE RESULTS
710 IF A(B,C)=1 GOTO 920
720 IF A(B,C)=2 GOTO 950
730 A(B,C)=3
740 E(6)=B
750 F(6)=C
760 FOR D=1 TO 5
770 IF A(E(D),F(D))<>2 GOTO 870
780 A(E(D),F(D))=0
790 IF E(D)<B THEN E(D)=E(D)+1
800 IF E(D)>B THEN E(D)=E(D)-1
810 IF F(D)<C THEN F(D)=F(D)+1
820 IF F(D)>C THEN F(D)=F(D)-1
830 IF A(E(D),F(D))=3 GOTO 950
840 IF A(E(D),F(D))<>0 THEN G=G-1
850 IF A(E(D),F(D))=0 THEN A(E(D),F(D))=2
860 IF G=0 GOTO 980
870 NEXT D
880 GOTO 410
890 PRINT:PRINT"SORRY TO SEE YOU QUIT"
900 PRINT"AFTER ONLY ";M;"MOVE";:IF M>1 THEN PRINT"S";:PRINT"."
910 PRINT:GOTO 1010
920 PRINT:PRINT"YOU TOUCHED THE FENCE !!!!!!!!"
930 PRINT" AFTER ONLY ";M;"MOVES."
940 GOTO 1010
950 PRINT:PRINT"** YOU HAVE BEEN DESTROYED BY A LUCKY COMPUTER**"
960 PRINT"AFTER ONLY ";M;" MOVES.   TOO BAD."
970 GOTO 1010
980 PRINT:PRINT"YOU ARE LUCKY * * YOU DESTROYED ALL THE ENEMY * *"
990 PRINT"IN ONLY ";M;" MOVES."
1000 Z2=Z2+1
1010 PRINT"SO FAR YOU HAVE WON ";Z2;" GAME(S)!!!"
1020 PRINT:PRINT:PRINT"WANT TO PLAY AGAIN(Y OR N)
1030 INPUT Y$
1040 PRINT:PRINT
1050 IF LEFT$(Y$,1)="Y" GOTO 180
1060 PRINT:PRINT"HOPE YOU DONT FEEL FENCED IN."
1070 PRINT"TRY AGAIN SOMETIME "
1080 PRINT"THIS TIME YOU WON ";Z2;" GAME(S)"
1090 Z2=Z2+1
1100 PRINT"NEXT TIME YOU MAY WIN ";Z2;", OR MORE!!!!!"
1110 END

1080 PRINT"THIS TIME YOU WON ";Z2;" GAME(S)"
1090 Z2=Z2+1
1100 PRINT"NEXT TIME YOU MAY WIN ";Z2;", OR MORE!!!!!"
111