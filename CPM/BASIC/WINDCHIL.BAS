10	REM ************** WINDCHIL.BAS **************
20	REM WRITTEN IN MICROSOFT BASIC BY MIKE LOZANO
30	REM CHIEF METEOROLOGIST, WHO TELEVISION, DES MOINES, IOWA
40	REM 1801 GRAND AVE. DES MOINES, IOWA 50308 (515)-242-3635
50	REM RELEASED TO THE PUBLIC DOMAIN FOR PERSONAL & NON-PROFIT USE.
60	REM DON'T CHANGE MATHEMATICAL RELATIONSHIP OF LINES
70	REM 180 & 210 OR YOU'LL GET FALSE RESULTS!
80	REM LINES 190 & 200 ARE THE FORMULA FOR WIND CHILL FACTOR.
90	PRINT CHR$(12):PRINT:PRINT: REM CHR$(12) CLEARS THE SCREEN
100	PRINT" WIND CHILL CALCULATOR ":PRINT:
110	INPUT" ENTER TEMPERATURE ",T: PRINT:
120	PRINT" (F) or (C)?":PRINT
130	T$=INKEY$: IF LEN(T$)=0 THEN GOTO 130
140	IF T$="F" THEN GOTO 170 
150	IF T$="C" THEN GOSUB 280:GOTO 170
160	PRINT CHR$(7);:GOTO 130
170	INPUT" ENTER WIND SPEED ",V:PRINT:
180	IF V>60 THEN V=60
190	A=6.686*SQR(V)-.447*V+10.45
200	WCI=91.4+A*(.04538*T-4.148)
210	IF V<5 THEN WCI=T ELSE IF WCI>T THEN GOTO 300
220	IF T$="C" THEN WCI=((5/9)*(WCI-32)) ELSE WCI=WCI:
230	PRINT " WIND CHILL = ";:PRINT USING "####.#";WCI;:PRINT" ";T$;: PRINT " DEGREES": PRINT:PRINT:
240	PRINT" ANOTHER RUN? Y/N ":
250	C$=INKEY$: IF LEN(C$)=0 THEN GOTO 250
260	IF C$="Y" THEN GOTO 20 ELSE IF C$="N" THEN END
270	PRINT CHR$(7);:GOTO 250
280	T=((9/5)*(T))+32
290	RETURN
300	PRINT" SORRY - BEYOND FORMULA LIMITS! ":PRINT:GOTO 240
310	REM ** FOR FREE WINDCHILL BROCHURE WHICH EXPLAINS WHAT WIND CHILL
320	REM ** ACTUALLY IS - HOW IT CAME TO BE - AND WHEN IT'S DANGEROUS
330	REM ** SEND S.A.S.E (BUSINESS SIZE ENVELOPE) TO ADDRESS ABOVE.
340	REM ** ENJOY!!!
