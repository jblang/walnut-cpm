100 ' SIMCVT.BAS: TRANSFORM THE SIMIBM.IDX FILE FROM SIMTEL20 INTO A
105 ' READABLE FORM, AND CALL IT SIMIBM.LST.
110 ' MODIFIED BY RQD, FEB 89 TO OUTPUT IN PRINTER FORM TO 
115 ' EITHER SCREEN OR PRINT FILE
116 ' FOR DOUBLE SIDE PRINTING USE BS.COM FROM HUG
120   FILES
130  INPUT "Enter Index Filename (.IDX) ";IFN$
140  OPEN IFN$ FOR INPUT AS #1
150  INPUT "Enter 1 FOR SCREEN, 2 FOR FILE OUTPUT ";INK
160  IF INK =1 GOTO 190
170  IF INK =2 GOTO 210
180  GOTO 150
190  OPEN "SCRN:" FOR OUTPUT AS #2
200  GOTO 230
210  DOT$="." : J=INSTR(IFN$,DOT$) :MID$(IFN$,J)=".LST"
220  OPEN IFN$ FOR OUTPUT AS #2
225  'OUTPUT FILE HAS SAME NAME WITH .IDX CHANGED TO .LST
230  PRINT #2, "WSMR-SIMTEL20.ARMY.MIL PUBLIC DOMAIN LISTING AS OF "; DATE$
240  PRINT #2,
250  PRINT #2, "NOTE: Type B is Binary; Type A is ASCII"
260  WHILE NOT EOF(1)
270   INPUT #1, FS$, DIR$, FLNM$, REV, LNGTH, BITS, DT, DESCR$
280   IF FS$<>LFS$ OR DIR$<>LDIR$ THEN GOSUB 380
290   PRINT #2, FLNM$; TAB(15);
300   IF BITS=8 THEN PRINT #2, "B"; ELSE PRINT #2, "A";
310   PRINT #2, USING " ####### ####### &"; LNGTH, DT, " "+DESCR$
320   LFS$ = FS$: LDIR$ = DIR$
330 WEND
340 CLOSE #1
350 CLOSE #2
360 STOP  :' SYSTEM
370 END
380 REM New Directory
390 PRINT #2,: PRINT #2, "Directory ";FS$;DIR$
400 PRINT #2, " Filename   Type Length   Date    Description"
410 PRINT #2, "=============================================="
420 RETURN
430 ' save"simcvt.bas",a
