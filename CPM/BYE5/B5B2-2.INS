; B2B2-1.ASM - Big Board II overlay file for BYE5 - 03/27/86
;
;		 Z80 SIO and 8430 CTC timer
;
; This overlay adapts BYE5 to the Big Board II computer with the SIO
; serial port "B" and the Z80 CTC baud rate generator.
;
;
; =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =
;
; 03/27/86  Corrected baudrate port	- Irv Hoff
; 10/27/85  Written for use with BYE5	- Irv Hoff
;
; =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =
;
; SIO Port A, Data = 80H, CTC = 89H
; SIO Port B, Data = 82H, CTC = 88H
;
PORT	EQU	80H	; Your Z80 SIO base port (data or status)
MDCTL1	EQU	PORT+1	; Modem control port
BRPORT	EQU	88H	; CTC port for baud rate
;
;
; Table of baud rate parameters
;
BD300	EQU	128		; 38400/300
BD1200	EQU	32		; 38400/1200
BD2400	EQU	16		; 38400/9600
BD9600	EQU	4		; 38400/9600
;
;
;-----------------------------------------------------------------------
;
; See if we still have a carrier - if not, return with the zero flag set
;
MDCARCK:MVI	A,10H		; Reset status
	OUT	MDCTL1
	IN	MDCTL1		; Get status
	ANI	08H		; Check for data carrier
	RET
;.....
;
;
; Disconnect and wait for an incoming call
;
MDINIT:	MVI	A,0		; Select default port
	OUT	MDCTL1
	MVI	A,18H		; Reset channel
	OUT	MDCTL1
	MVI	A,4		; Setup to write register 4
	OUT	MDCTL1
	MVI	A,44H		;*1 stop, 8 bits, no parity, 16x
	OUT	MDCTL1
	MVI	A,5		; Setup to write register 5
	OUT	MDCTL1
	MVI	A,68H		; Clear RTS causing hangup
	OUT	MDCTL1
	PUSH	B		; Save in case it's being used elsewhere
	MVI	B,20		; 2 seconds delay to drop any carrier
;
OFFTI:	CALL	DELAY		; 0.1 second delay
	DCR	B
	JNZ	OFFTI		; Keep looping until finished
	POP	B		; Restore bc
	MVI	A,3		; Setup to write register 3
	OUT	MDCTL1
	MVI	A,0C1H		; Initialize receive register
	OUT	MDCTL1
	MVI	A,5		; Setup to write register 5
	OUT	MDCTL1
	MVI	A,0EAH		; Turn on RTS so modem can answer phone
	OUT	MDCTL1
;
	 IF	IMODEM		; If using intelligent modem
	CALL	IMINIT		; Go initialize
	 ENDIF			; IMODEM
;
	RET
;.....
;
;
; Input a character from the modem port
;
MDINP:	IN	PORT		; Get character
	RET
;.....
;
;
; Check the status to see if a character is available.	If not, return
; with the zero flag set.  If yes, use 0FFH to clear the flag.
;
MDINST:	IN	MDCTL1		; Get status
	ANI	01H		; Check receive ready bit
	RZ			; Return if none
	ORI	0FFH		; Otherwise, set the proper flag
	RET
;.....
;
;
; Send a character to the modem
;
MDOUTP:	OUT	PORT		; Send it
	RET
;.....
;
;
; See if the output is ready for another character
;
MDOUTST:IN	MDCTL1
	ANI	04H		; Check transmit ready bit
	RET
;.....
;
;
; Reinitialize the modem and hang up the phone by dropping DTR and
; leaving it inactive.
;
MDQUIT:	 IF	IMODEM		; If using a smartmodem
	CALL	IMQUIT		; Tell it to shut down
	 ENDIF			; IMODEM
;
;
; Called by the main program after caller types BYE
;
MDSTOP:	MVI	A,5		; Setup to write register 5
	OUT	MDCTL1
	MVI	A,68H		; Clear RTS causing shutdown
	OUT	MDCTL1
	RET
;.....
;
;
; The following routine sets the baudrate.  BYE5 asks for the maximum
; speed you have available.
;
SETINV:	ORI	0FFH		; Make sure zero flag is not set
	RET			; Return
;.....
;
;
SET300:	MVI	B,BD300		; Get 300 bps parameters in 'HL'
	JMP	LOADBD		; Go load them
;
SET1200:MVI	B,BD1200
	JMP	LOADBD
;
SET2400:MVI	B,BD2400
	JMP	LOADBD
;
SET9600:MVI	B,BD9600
;
LOADBD:	MVI	A,47H		; CTC command word
	OUT	BRPORT
	XTHL			; Short delay
	XTHL			; Additional short delay
	MOV	A,B		; Baudrate
	OUT	BRPORT		; Reset CTC to new baudrate
	XRA	A		; Say baudrate was ok
	RET
;.....
;
;			       end
;-----------------------------------------------------------------------
