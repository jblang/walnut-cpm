
; B5MT-1.INS - Memotech SDX-512 computer insert for BYE5 - 02/11/86
;
;		DART I/O and CTC timer 4.000 MHz
;
; Note:  This is an insert, not an overlay.
;
; This version is for the Memotech SDX-512 using channel "B" of the Z80
; DART.  The Z80-CTC is used as the baud rate generator.
;
; NOTE:  The carrier detect input is checked for "Active low" which
;	 is quite unusual, and requires a "CMA" in MDCARCK.
;
;	   Make up a RS232 cable per the instructions below:
;
;			Modem	   Ampro
;			-----	   -----
;			pin 2	-  pin 2
;			pin 3	-  pin 3
;			pin 7	-  pin 7
;			pin 8	-  pin 8
;			pin 20	-  pin 20
;
;
; =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =
;
; 02/11/86  Initial Memotech version		- Irv Hoff
;
; =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =   =
;
;-----------------------------------------------------------------------
;
; Values given are for port 'B'
;
PORT	EQU	0DH		; Data port
MDCTL1	EQU	0FH		; Status/control port
BRPORT	EQU	0AH		; Baud rate port
;
;
;-----------------------------------------------------------------------
;
; See if we still have a carrier - if not, return with the zero flag set
;
MDCARCK:MVI	A,10H		; Reset status
	OUT	MDCTL1
	IN	MDCTL1		; Get status
	CMA			; Invert for "active low" for Memotech
	ANI	20H		; Check for data carrier
	RET
;.....
;
;
; Disconnect and wait for an incoming call
;
MDINIT:	MVI	A,18H		; Reset channel
	OUT	MDCTL1
	MVI	A,4		; Setup to write register 4
	OUT	MDCTL1
	MVI	A,44H		;*1 stop, 8 bits, no parity, 16x
	OUT	MDCTL1
	MVI	A,5		; Setup to write register 5
	OUT	MDCTL1
	MVI	A,68H		; Clear RTS causing hangup
	OUT	MDCTL1
	PUSH	B		; Save in case it's being used elsewhere
	MVI	B,20		; 2 seconds delay to drop any carrier
;
OFFTI:	CALL	DELAY		; 0.1 second delay
	DCR	B
	JNZ	OFFTI		; Keep looping until finished
	POP	B		; Restore bc
	MVI	A,3		; Setup to write register 3
	OUT	MDCTL1
	MVI	A,0C1H		; Initialize receive register
	OUT	MDCTL1
	MVI	A,5		; Setup to write register 5
	OUT	MDCTL1
	MVI	A,0EAH		; Turn on RTS so modem can answer phone
	OUT	MDCTL1
;
	 IF	IMODEM		; If using intelligent modem
	CALL	IMINIT		; Go initialize
	 ENDIF			; IMODEM
;
	RET
;.....
;
;
; Input a character from the modem port
;
MDINP:	IN	PORT		; Get character
	RET
;.....
;
;
; Check the status to see if a character is available.	If not, return
; with the zero flag set.  If yes, use 0FFH to clear the flag.
;
MDINST:	IN	MDCTL1		; Get status
	ANI	01H		; Check receive ready bit
	RZ			; Return if none
	ORI	0FFH		; Otherwise, set the proper flag
	RET
;.....
;
;
; Send a character to the modem
;
MDOUTP:	OUT	PORT		; Send it
	RET
;.....
;
;
; See if the output is ready for another character
;
MDOUTST:IN	MDCTL1
	ANI	04H		; Check transmit ready bit
	RET
;.....
;
;
; Reinitialize the modem and hang up the phone by dropping DTR and
; leaving it inactive.
;
MDQUIT:	 IF	IMODEM		; If using a smartmodem
	CALL	IMQUIT		; Tell it to shut down
	 ENDIF			; IMODEM
;
;
; Called by the main program after caller types BYE
;
MDSTOP:	MVI	A,5		; Setup to write register 5
	OUT	MDCTL1
	MVI	A,68H		; Clear RTS causing shutdown
	OUT	MDCTL1
	RET
;.....
;
;
; The following routine sets the baudrate.  BYE5 asks for the maximum
; speed you have available.
;
SETINV:	ORI	0FFH		; Make sure zero flag is not set
	RET			; Return
;.....
;
;
SET300:	MVI	B,BD300		; Get 300 bps parameters in 'HL'
	JMP	LOADBD		; Go load them
;
SET1200:MVI	B,BD1200
	JMP	LOADBD
;
SET2400:MVI	B,BD2400
	JMP	LOADBD
;
SET9600:MVI	B,BD9600
;
LOADBD:	MVI	A,47H		; CTC command word
	OUT	BRPORT
	MOV	A,B		; Baudrate
	OUT	BRPORT
	XRA	A
	RET
;.....
;
;
BD300	EQU	64		; 19200/300  (first half is CTC command)
BD1200	EQU	16		; 19200/1200
BD2400	EQU	8		; 19200/2400
BD9600	EQU	2		; 19200/9600
;
;			       end
;-----------------------------------------------------------------------
