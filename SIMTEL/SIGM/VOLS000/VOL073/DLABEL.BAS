100 '	DISK LABLE GENERATING PROGRAM,
101 '	BILL BOLTON, Software Tools, Australia
102 '	27/May/80
103 '
105 GOSUB 6000				'SET PRINTER TO DAISY
110 GOSUB 3000				'CLEAR SCREEN
120 '
130 PRINT TAB(24);
140 GOSUB 3030				'SET REVERSE VIDEO
150 '
160 PRINT " DISK LABLE PRINTING PROGRAM ";
170 '
180 GOSUB 3060				'CLEAR REVERSE VIDEO
190 PRINT
200 PRINT
210 GOSUB 1110				'PRINT THE FIRST PROMPT
220 GOSUB 1150				'PRINT THE SECOND PROMPT
230 GOSUB 1190				'PRINT THE THIRD PROMPT
240 GOSUB 1230				'PRINT THE FOURTH PROMPT
250 PRINT
260 GOSUB 4000				'DISPLAY THE COMPLETE LABLE
270 '
280 LINE INPUT "Do you want to change a line ? (Y/N, CR = N) ";E$
290 '
300 IF E$="Y" OR E$="y" THEN 1000
310 PRINT
320 PRINT
330 '
340 LINE INPUT "Line up the lable in the printer and press CR when ready to print";Z$
350 '
360 LPRINT A$				'PRINT THE FIRST LINE ON THE LABLE
370 LPRINT
380 LPRINT B$				'PRINT THE SECOND LINE ON THE LABLE
390 LPRINT 
400 LPRINT C$				'PRINT THE THIRD LINE ON THE LABLE
410 LPRINT 
420 LPRINT D$				'PRINT THE FOURTH LINE ON THE LABLE
430 LPRINT
440 LPRINT
450 PRINT
460 PRINT
470 '
480 LINE INPUT "Another copy of the lable ? (Y/N, CR = N) ";F$
490 '
500 IF F$="Y" OR F$="y" THEN 260
510 PRINT
520 PRINT
530 '
540 LINE INPUT "A new lable to print ? (Y/N, CR = Y) ";G$
550 '
560 IF G$="N" OR G$="n" THEN GOTO 600 ELSE GOTO 110
600 GOSUB 3000
610 GOSUB 6100
620 'PRINT "Printer has been set to Daisywheel, reset if necessary with STAT";CHR$(7)
630 END
640 '
650 '
1000 PRINT
1010 PRINT
1020 '
1030 INPUT "Which line do you want to change (1,2,3,4) ";L
1040 '
1050 IF L=1 THEN GOSUB 1110
1060 IF L=2 THEN GOSUB 1150
1070 IF L=3 THEN GOSUB 1190
1080 IF L=4 THEN GOSUB 1230
1090 IF L<1 OR L>4 THEN GOTO 2000 ELSE GOTO 260
1100 '
1110 PRINT
1120 LINE INPUT "Enter the first line  : ";A$
1130 RETURN
1140 '
1150 PRINT
1160 LINE INPUT "Enter the second line : ";B$
1170 RETURN
1180 '
1190 PRINT
1200 LINE INPUT "Enter the third line  : ";C$
1210 RETURN
1220 '
1230 PRINT
1240 LINE INPUT "Enter the fourth line : ";D$
1250 RETURN
1260 '
1270 '
2000 PRINT
2010 PRINT
2020 PRINT CHR$(7);			'RING THE TERMINAL BELL
2030 '
2040 GOSUB 3030				'SET REVERSE VIDEO
2050 '
2060 PRINT "*********    Answer 1 to 4 only, please    *********";
2070 '
2080 GOSUB 3060				'CLEAR REVERSE VIDEO
2090 PRINT
2100 PRINT
2110 GOSUB 5000				'DISPLAY CONTINUE MESSAGE
2120 GOSUB 4000				'DISPLAY COMPLETE LABLE
2130 GOTO 1010
2140 '
2150 '
3000 PRINT CHR$(27);CHR$(42)		'CLEAR SCREEN ROUTINE
3010 RETURN
3020 '
3030 PRINT CHR$(27);"G4";		'SET REVERSE VIDEO ROUTINE
3040 RETURN
3050 '
3060 PRINT CHR$(27);"G0";		'CLEAR REVERSE VIDEO ROUTINE
3070 RETURN
3080 '
3090 '
4000 GOSUB 3000				'CLEAR THE SCREEN
4010 PRINT "Your disk lable will look like this :"
4020 PRINT
4030 PRINT
4040 PRINT A$				'DISPLAY THE FIRST LINE
4050 PRINT
4060 PRINT B$				'DISPLAY THE SECOND LINE
4070 PRINT
4080 PRINT C$				'DISPLAY THE THIRD LINE
4090 PRINT
4100 PRINT D$				'DISPLAY THE FOURTH LINE
4110 PRINT
4120 PRINT
4130 PRINT
4140 RETURN
4150 '
4160 '
5000 PRINT
5010 LINE INPUT "Hit 'Return' to continue";Z$
5020 RETURN
5030 '
5040 '
6000 IOBYTE = PEEK(3)
6010 MASK = 63
6020 DAISY = 64
6030 POKE 3,(DAISY + (IOBYTE AND MASK))
6040 RETURN
6050 '
6060 '
6100 POKE 3,IOBYTE
6110 RETURN
6120 '
6130 END
