8002 '	comment by system
8004 '	comment in COMMENT$
8006 '	1.3
8008 SETUSERNUMBER%=USERNUMBER%: GOSUB 300
8010 OPEN "R", #3, DEFDRIVE$+COMMENTFILE$, 66
8012 FIELD #3,
	5 AS COMMENTNUMBER$,
	8 AS TOTCOMMT$,
	1 AS COMSIG$,
	6 AS FCOMDATE$,
	6 AS FCOMTIME$,
	1 AS FINEWCOM$,
	1 AS FSYSCOM$
8014 GET #3,1
8016 NEXTCOMMENT%=VAL(COMMENTNUMBER$)
8018 TOTALCOMMENTS#=VAL(TOTCOMMT$)
8020 COMTIME$=FCOMTIME$
8022 COMDATE$=FCOMDATE$
8024 NEWCOM$=FINEWCOM$
8026 SYSCOM$=FSYSCOM$
8028 IF COMSIG$<>"*" THEN 
	NEXTCOMMENT%=2:
	TOTALCOMMENTS#=1:
	GOSUB 1600:
	COMTIME$=TIME$:
	COMDATE$=DATE$
8030 FIELD #3, 64 AS PCOMMENT$, 2 AS CRLF$: LSET CRLF$=CHR$(13)+CHR$(10)
8032 GOSUB 1600
8034 ANSWER$="~"+STR$(TOTALCOMMENTS#)+" while "+FRNAME$+" "+LNAME$
	+" on " +FNADDSEP$(DATE$,"/")+" at "+FNADDSEP$(TIME$,":")
	+" ("+NOTATION$+")"
8036 GOSUB 8064	'put comment line
8038 ANSWER$=COMMENT$+" in module: "+MODNAME$
8040 GOSUB 8064
8044 LSET COMMENTNUMBER$ = STR$(NEXTCOMMENT%)
8046 LSET TOTCOMMT$ = STR$(TOTALCOMMENTS#+1)
8048 LSET COMSIG$="*"
8050 LSET FCOMDATE$ = COMDATE$
8052 LSET FCOMTIME$ = COMTIME$
8054 LSET FINEWCOM$=NEWCOM$
8056 LSET FSYSCOM$="*"
8058 PUT #3,1
8060 CLOSE #3
8062 RETURN
8064 LSET PCOMMENT$ = ANSWER$
8066 PUT #3, NEXTCOMMENT%
8068 NEXTCOMMENT%=NEXTCOMMENT%+1
8070 RETURN
