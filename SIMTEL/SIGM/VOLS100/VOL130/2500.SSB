2501 '	display userfile$
2502 '1.16	#
2503 GOSUB 3100
2504 LNCNT%=0: NOPAGE%=0
2505 PRINT: PRINT
2506 GOSUB 1400
2507 PRINT TAB(10); "Press RETURN or type selection information:"
2508 PRINT TAB(10); "Where:"
2509 PRINT: PRINT TAB(10);"123p;4"
2510 PRINT: PRINT TAB(10);"     ^--access level to select
2511 PRINT TAB(10);"   ^----p to skip paging every";PAGESIZE%;"lines."
2512 PRINT TAB(10);
	"^^^-----record number to start at (";NEXTUSER-2;"is highest)."
2513 PRINT: PRINT TAB(10);"All information is optional."
2514 MAX%=7: GOSUB 500
2515 IF RIGHT$(ANSWER$,1)="P" THEN NOPAGE%=1:
	IF NKEY% > 1 THEN NKEY%=NKEY%-1: ANSWER$=LEFT$(ANSWER$,NKEY%)
2516 IF NKEY%=0 THEN INDEX=1 ELSE INDEX=VAL(ANSWER$)
2517 IF INDEX < 1 THEN INDEX = 1
2518 IF INDEX => NEXTUSER THEN INDEX = NEXTUSER-10 
2519 BEGINING% = INDEX - 1
2520 IF STACKED%<>0 THEN GOSUB 500: SELECTACL%=VAL(ANSWER$): 
	SELMESS$="for access level"+STR$(SELECTACL%)+" "
	ELSE SELECTACL%=-1: SELMESS$=""
2521 GOSUB 3100
2522 HEADER$=
"rec    first last, location            acl uses      last use      total time"+CRLF$
2523 COUNTER%=0
2524 COUNTER1%=0
2525 SAVEDACLVL%=ACLVL%
2526 PRINT TAB(20);"Press ^K to abort listing."
2527 PRINT HEADER$
2528 GET #1,INDEX+1
2529	IF EOF(1) OR INDEX=NEXTUSER-1 THEN GOTO 2542
2530	GOSUB 1300	'transfer to working vars
2531	IF RIGHT$(LOCATION$,1)=" " THEN
		LOCATION$ = LEFT$(LOCATION$,LEN(LOCATION$)-1): GOTO 2531
2532	IF LNCNT%=>PAGESIZE% THEN A$=HEADER$: GOSUB 400:
	A$="[ Press RETURN for more "+SELMESS$+"]": GOSUB 400:
	MAX%=0: GOSUB 500: PRINT CHR$(&HD);: LNCNT%=0
2533	IF SELECTACL%=ACLVL% OR SELECTACL%=-1 THEN
	IF DELETED%=1 THEN LOCATION$= "**deleted**":
		 COUNTER%=COUNTER%+1
		ELSE 
		 COUNTER1%=COUNTER1%+1
2534	ANSWER$="      ": RSET ANSWER$=FNHOURS$(TOTALTIME)
2535	IF SELECTACL%=ACLVL% OR SELECTACL%=-1 THEN
	PRINT USING "### \                                  \## ###  & &   & ";
		INDEX;FRNAME$+" "+LNAME$+", "+LOCATION$;ACLVL%;
		SIGCNT;FNADDSEP$(LASTDATE$,"/");
		FNADDSEP$(LASTTIME$,":");
		ANSWER$
2536	KEY$=INKEY$: IF KEY$="S" OR KEY$="s" OR KEY$=CHR$(&H13)
		THEN PAUSE%=1 ELSE PAUSE%=0
2537	IF KEY$=CHR$(&HB) THEN ABORT%=1: GOTO 2542
		ELSE ABORT%=0
2538	IF PAUSE%<>0 AND LEN(INKEY$)=0 THEN GOTO 2538
2539	IF NOPAGE%=0
	AND (SELECTACL%=ACLVL% OR SELECTACL%=-1)
	THEN LNCNT%=LNCNT%+1
2540	INDEX=INDEX+1
2541 GOTO 2528
2542 PRINT
2543 IF VIEWFILE$="" THEN PRINT "User file reorganized";
	ELSE PRINT "User archive made";: VIEWFILE$=""
2544 PRINT " on ";FNADDSEP$(UDATE$,"/"); " at ";FNADDSEP$(UTIME$,":");"."
2545 PRINT
2546 IF ABORT% THEN PRINT
  TAB(35);"** Aborted **    (totals based on displayed data only)"+CRLF$
2547 IF BEGINING%<>0 THEN PRINT "For displayed data only:"
2548 IF SELECTACL%=-1 THEN ANSWER$="" ELSE
	ANSWER$="for access level"+STR$(SELECTACL%)
2549 PRINT " Active Users: ";COUNTER1%
2550 PRINT "Deleted Users: ";COUNTER%
2551 PRINT "Total Entries: ";COUNTER1%+COUNTER%; SELMESS$
2552 GOSUB 4700
2553 CLOSE #1
2554 ACLVL%=SAVEDACLVL%
2555 RETURN
