
[BINSU.PST]
[Post version of binary sum]

[To sum two binary numbers using Post's Productions, we set up
 two addition tables for the terminal digits of the numbers.
 One table is used initially, and when there is no carry present.
 The other is used to incorporate a propagated carry into the
 sum.  Auxiliary symbols are naturally incorporated into the
 sum by writing the Axiom in the form a+b=c. The digits to be
 removed from a and b are identified by their proximity to + and
 =, respectively.  As the sum is developed, its digits are inserted
 to the right of the = sign. The computation terminates when the
 auxiliary symbols are dropped after at least one of them
 reaches the front of its summand.]

[Post Productions can deal with the digits to be summed even
 though they are widely separated.  In a Markov Algorithm it is
 necessary to move one of them until it is in contact with the
 other before the addition table can be applied.]

[[
A Post Production scheme which will sum two binary
numbers written in the form a+b=c; for example

          111110+011=

Each successive keystroke will display one more step
in the transformation until the sum is finished.
]]



(<1>0+<2>0=<3>,<1>+<2>=0<3>)
(<1>0+<2>1=<3>,<1>+<2>=1<3>)
(<1>1+<2>0=<3>,<1>+<2>=1<3>)
(<1>1+<2>1=<3>,<1>+<2>*0<3>)
(<1>0+<2>0*<3>,<1>+<2>=1<3>)
(<1>0+<2>1*<3>,<1>+<2>*0<3>)
(<1>1+<2>0*<3>,<1>+<2>*0<3>)
(<1>1+<2>1*<3>,<1>+<2>*1<3>)
(<1>+<2>=<3>,<1><2><3>)
(<1>+<2>*<3>,0<1>+0<2>*<3>)

[end]
