	REM -------FILE VOCLST.BAS

	REM VOCABULARY LIST TO CONSOLE/PRINTER
	COMMON KEYWD$(1),LIBNAME$,RLEN%,TRUE%,AUTH.LEN%,TITL.LEN%
	COMMON MAXBIB%,MAXDESC%,MAXDEF%,MAXKEYS%,MAXCON%,CONCEPT$(1)
	COMMON ISS.LEN%,CLEAR$,ERR%,JOUR.LEN%,KWD.LEN%,CMD$(1)
	COMMON LFT%(1),RGHT%(1),CON.KEY%(2),CON.RATE(2),QUERY$,L%

	PRINT CLEAR$ : PRINT
	PRINT TAB(12);"----------KEYWORD VOCABULARY LISTER----------":PRINT
	INPUT "DO YOU WISH A PRINTED LISTING (Y/N) ";LINE ANS$
	IF UCASE$(LEFT$(ANS$,1))="Y" THEN LPRINTER:COL%=4 ELSE \
	   PRINT CLEAR$:COL%=3

	REM IF NO VOCABULARY FILE RETURN TO MENU
	IF END #1 THEN 3.3E1
	OPEN LIBNAME$+".VOC" AS 1
	IF SIZE(LIBNAME$+".VOC")=0 THEN DELETE 1 : GOTO 3.3E1

	REM OTHERWISE READ COMPLETE VOCABULARY
	IF END #1 THEN 3.1E1
	I%=1
	WHILE TRUE%
  	READ #1;LINE KEYWD$(I%)
  	I%=I%+1
  	WEND

3.1E1   I%=I%-1:CLOSE 1

	REM LIST IN FOUR COLUMNS FOR PRINTER, THREE FOR SCREEN
	IF COL%=4 THEN LPRINTER:FOR J%=1 TO 5:PRINT:NEXT J%: \
  	   PRINT TAB(23);LIBNAME$;" KEYWORD VOCABULARY" : \
  	   PRINT TAB(23);"---------------------------"
	N.ROW%=INT%(I%/COL%)
	IF I%>(N.ROW%*COL%) THEN N.ROW%=N.ROW%+1
	FOR J%=1 TO N.ROW%
  	FOR K%=0 TO COL%-1
    	IND%=J%+K%*N.ROW%
    	IF IND%>I% THEN 3.2E1
    	PRINT USING "###"; IND%;:PRINT " "+KEYWD$(IND%);
    	NEXT K%
  
3.2E1   PRINT
  	NEXT J%
	PRINT
	IF COL%=4 THEN PRINT:PRINT:PRINT:PRINT
	CONSOLE:PRINT

3.4E1   INPUT "PRESS RETURN TO GO BACK TO MENU"; LINE ANS$
	CHAIN "BIBLIO"

3.3E1   PRINT "NO VOCABULARY FILE NAMED "; LIBNAME$:GOTO 3.4E1

