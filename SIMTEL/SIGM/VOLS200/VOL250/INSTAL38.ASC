1  ' RBBS38 Install Program
3  ' 
4  ' Lillypond Softwares   Dennis Recla 
6  '		(07/21/85)
8  '  
10 ERS$=CHR$(8)+" "+CHR$(8)
20 BSL$=CHR$(8)+"/"+CHR$(8)
30 DEF FNCT$(C$,SW)=STRING$(INT((SW-LEN(C$))/2)," ")+C$
40 ' 
50 PRINT:PRINT
60 VERS$="RBBS NSTALLATION PROGRAM v 3.8 (07/21/85)"
70 PRINT FNCT$(VERS$,80)
80 PRINT:PRINT
90 PRINT "          You will be asked several questions reguarding the way you"
100 PRINT "          will want the RBBS program to operate on your system."
110 PRINT
120 PRINT "          Answer the questions or select the proper choices from the"
130 PRINT "          menu that is provided.  This Installation program must be"
140 PRINT "          run on the drive/user area that you will want the RBBS"
150 PRINT "          program to reside.  Other drives can be assigned for the"
160 PRINT "          files required or created, but they must all be in the same"
170 PRINT "          user area. (i.e. user 14)  The RBBS Program does not allow"
180 PRINT "          for files to exist in other user areas, but will provide for"
190 PRINT "          alternate drives to be used in cases where your system has"
200 PRINT "          small capacity disk drives."
210 PRINT:PRINT
220 GOSUB 3480:GOTO 3670
230 PRINT:PRINT
240 PRINT " IF you need help then type a ? in place of an answer."
250 PRINT " Type a - if you need to go back to the last question."
260 PRINT:PRINT:GOSUB 4250
270 ' 
280 A1$="Enter drive to be used for DSK2$ files: ":N=1:GOSUB 2760
290 C=1:GOSUB 2890
300 IF B$="-" THEN 260
310 IF B$="?" THEN HLP=1:GOSUB 3560:GOTO 260
320 SL=1:GOSUB 3510:IF RTN=0 THEN 260 ELSE DSK2$=B$+":"
330 PRINT:PRINT:GOSUB 4250
340 ' 
350 A1$="Enter drive to be used for DSK3$ files: ":N=1:GOSUB 2760
360 C=1:GOSUB 2890
370 IF B$="-" THEN 260
380 IF B$="?" THEN HLP=2:GOSUB 3560:GOTO 330
390 SL=1:GOSUB 3510:IF RTN=0 THEN 330 ELSE DSK3$=B$+":"
400 PRINT:PRINT:GOSUB 4250
410 ' 
420 A1$="Enter your FIRST Name: ":N=1:GOSUB 2760
430 C=1:GOSUB 2890
440 IF B$="-" THEN 330
450 IF B$="?" THEN HLP=3:GOSUB 3560:GOTO 400
460 SYS1$=B$
470 PRINT:PRINT:GOSUB 4250
480 ' 
490 A1$="Enter your LAST Name: ":N=1:GOSUB 2760
500 C=1:GOSUB 2890
510 IF B$="-" THEN 400
520 IF B$="?" THEN HLP=4:GOSUB 3560:GOTO 470
530 SYS2$=B$
540 PRINT:PRINT:GOSUB 4250
550 ' 
560 A1$="Enter the name of your RBBS System: ":GOSUB 2760:PRINT
570 C=0:GOSUB 2890
580 IF B$="-" THEN 470
590 IF B$="?" THEN HLP=5:GOSUB 3560:GOTO 540
600 VERS1$=B$
610 PRINT:PRINT:GOSUB 4250
620 ' 
630 A1$="Enter your direct-to-CP/M FIRST Name password: ":N=1:GOSUB 2760
640 C=1:GOSUB 2890
650 IF B$="-" THEN 540
660 IF B$="?" THEN HLP=6:GOSUB 3560:GOTO 610
670 P1$=B$
680 PRINT:PRINT:GOSUB 4250
685 ' 
690 A1$="Enter your LAST Name password for SYSOP: ":N=1:GOSUB 2760
695 C=1:GOSUB 2890
700 IF B$="-" THEN 610
705 IF B$="?" THEN HLP=7:GOSUB 3560:GOTO 680
708 P2$=B$
710 PRINT:PRINT:GOSUB 4250
715 '
720 A1$="Set the WHEEL for SYSOP on exit to CP/M (Y/N) ":N=1:GOSUB 2760
725 C=1:GOSUB 2890
730 IF B$="-" THEN 680
735 IF B$="?" THEN HLP=8:GOSUB 3560:GOTO 710
740 IF LEFT$(B$,1)="Y" THEN WHEEL=-1:GOTO 750
745 WHEEL=0
750 PRINT:PRINT:GOSUB 4250
760 '
770 A1$="Enter your USER Validation password: ":N=1:GOSUB 2760
780 C=1:GOSUB 2890
790 IF B$="-" THEN 710
800 IF B$="?" THEN HLP=9:GOSUB 3560:GOTO 750
810 VAP$=B$
820 PRINT:PRINT:GOSUB 4250
830 ' 
840 A1$="Enter your CP/M entry question: ":N=1:GOSUB 2760:PRINT
850 C=0:GOSUB 2890
860 IF B$="-" THEN 750
870 IF B$="?" THEN HLP=10:GOSUB 3560:GOTO 820
880 PC$=B$
890 PRINT:PRINT:GOSUB 4250
900 ' 
910 A1$="Enter the answer to your CP/M entry question: ":N=1:GOSUB 2760
920 C=1:GOSUB 2890
930 IF B$="-" THEN 820
940 IF B$="?" THEN HLP=11:GOSUB 3560:GOTO 890
950 P3$=B$
960 PRINT:PRINT:GOSUB 4250
970 ' 
980 A1$="More than one SYSOP? (Y/N) ":N=1:GOSUB 2760
990 C=1:GOSUB 2890
1000 IF B$="-" THEN 890
1010 IF B$="?" THEN HLP=12:GOSUB 3560:GOTO 960
1020 IF LEFT$(B$,1)="Y" THEN MSYS=-1:GOTO 1040
1030 MSYS=0
1040 PRINT:PRINT:GOSUB 4250
1050 ' 
1060 A1$="Bell delay value (try 20000): ":N=1:GOSUB 2760
1070 C=1:GOSUB 2890
1080 IF B$="-" THEN 960
1090 IF B$="?" THEN HLP=13:GOSUB 3560:GOTO 1040
1100 BEEP=VAL(B$)
1110 PRINT:PRINT:GOSUB 4250
1120 ' 
1130 A1$="Number of lines allowed in messages (try 15): ":N=1:GOSUB 2760
1140 C=1:GOSUB 2890
1150 IF B$="-" THEN 1040
1160 IF B$="?" THEN HLP=14:GOSUB 3560:GOTO 1110
1170 SIZE=VAL(B$)
1175 IF SIZE>25 THEN A$="Sorry more than 25 cause problems " ELSE 1180
1178 N=1:GOSUB 2760:PRINT:GOTO 1130
1180 PRINT:PRINT:GOSUB 4250
1190 ' 
1200 A1$="Number of NEWS files: ":N=1:GOSUB 2760
1210 C=1:GOSUB 2890
1220 IF B$="-" THEN 1110
1230 IF B$="?" THEN HLP=15:GOSUB 3560:GOTO 1180
1240 NNUM=VAL(B$)
1250 PRINT:PRINT:GOSUB 4250
1260 ' 
1270 A1$="Which drive contains your NEWS files: ":N=1:GOSUB 2760
1280 C=1:GOSUB 2890
1290 IF B$="-" THEN 1180
1300 IF B$="?" THEN HLP=16:GOSUB 3560:GOTO 1250
1310 SL=1:GOSUB 3510:IF RTN=0 THEN 1250 ELSE DSK5$=B$+":"
1320 PRINT:PRINT:GOSUB 4250
1330 ' 
1340 A1$="Number of HELP files: ":N=1:GOSUB 2760
1350 C=1:GOSUB 2890
1360 IF B$="-" THEN 1250
1370 IF B$="?" THEN HLP=17:GOSUB 3560:GOTO 1320
1380 HNUM=VAL(B$)
1390 PRINT:PRINT:GOSUB 4250
1400 ' 
1410 A1$="Which drive contains your HELP files: ":N=1:GOSUB 2760
1420 C=1:GOSUB 2890
1430 IF B$="-" THEN 1320
1440 IF B$="?" THEN HLP=18:GOSUB 3560:GOTO 1390
1450 SL=1:GOSUB 3510:IF RTN=0 THEN 1390 ELSE DSK4$=B$+":"
1460 PRINT:PRINT:GOSUB 4250
1470 ' 
1480 A1$="Skip (Last Message Read) info? (Y/N) ":N=1:GOSUB 2760
1490 C=1:GOSUB 2890
1500 IF B$="-" THEN 1390
1510 IF B$="?" THEN HLP=19:GOSUB 3560:GOTO 1460
1520 IF LEFT$(B$,1)="Y" THEN SKIP=-1:GOTO 1540
1530 SKIP=0
1540 PRINT:PRINT:GOSUB 4250
1550 ' 
1560 A1$="Logout TWITs? (Y/N) ":N=1:GOSUB 2760
1570 C=1:GOSUB 2890
1580 IF B$="-" THEN 1460
1590 IF B$="?" THEN HLP=20:GOSUB 3560:GOTO 1540
1600 IF LEFT$(B$,1)="Y" THEN TWIT=-1:GOTO 1620
1610 TWIT=0
1620 PRINT:PRINT:GOSUB 4250
1630 ' 
1640 A$="Can <E>veryone enter messages, both <V>alid and Super"
1650 N=1:GOSUB 2760
1660 A$="users, or just <S>uper users?"
1670 N=1:GOSUB 2760
1680 A1$="  Enter (E, V or S): ":N=1:GOSUB 2760
1690 C=1:GOSUB 2890
1700 IF B$="-" THEN 1540
1710 IF B$="?" THEN HLP=21:GOSUB 3560:GOTO 1620
1720 BT$=LEFT$(B$,1)
1730 IF BT$="E" THEN LMSG=1:GOTO 1760
1740 IF BT$="V" THEN LMSG=2:GOTO 1760
1750 IF BT$="S" THEN LMSG=3
1760 PRINT:PRINT:GOSUB 4250
1770 ' 
1780 A$="Can <E>veryone have passworded CP/M access, both <V>alid"
1790 N=1:GOSUB 2760
1800 A$="and Super users, or just <S>uper users?"
1810 N=1:GOSUB 2760
1820 A1$="  Enter (E, V or S): ":N=1:GOSUB 2760
1830 C=1:GOSUB 2890
1840 IF B$="-" THEN 1620
1850 IF B$="?" THEN HLP=22:GOSUB 3560:GOTO 1760
1860 BT$=LEFT$(B$,1)
1870 IF BT$="E" THEN GOCPM=1:GOTO 1900
1880 IF BT$="V" THEN GOCPM=2:GOTO 1900
1890 IF BT$="S" THEN GOCPM=3
1900 PRINT:PRINT:GOSUB 4250
1910 ' 
1920 A1$="Do you have an external clock? (Y/N) ":N=1:GOSUB 2760
1930 C=1:GOSUB 2890
1940 IF B$="-" THEN 1760
1950 IF B$="?" THEN HLP=23:GOSUB 3560:GOTO 1900
1960 IF LEFT$(B$,1)="Y" THEN DATIM=-1:GOTO 1980
1970 DATIM=0
1980 PRINT:PRINT:GOSUB 4250
1990 ' 
2000 A$="Enter the filename you want to be loaded at Goodbye: ":N=1:GOSUB 2760
2010 C=1:GOSUB 2890
2020 IF B$="-" THEN 1900
2030 IF B$="?" THEN HLP=24:GOSUB 3560:GOTO 1980
2040 EXIT$=B$
2050 PRINT:PRINT:GOSUB 4250
2060 ' 
2070 A$="Show location of callers and users? (Y/N) ":N=1:GOSUB 2760
2080 C=1:GOSUB 2890
2090 IF B$="-" THEN 1980
2100 IF B$="?" THEN HLP=25:GOSUB 3560:GOTO 2050
2110 IF LEFT$(B$,1)="Y" THEN SHOLOC=-1:GOTO 2130
2120 SHOLOC=0
2130 PRINT:PRINT:GOSUB 4250
2140 ' 
2150 A$="Log unvalidated callers? (Y/N) ":N=1:GOSUB 2760
2160 C=1:GOSUB 2890
2170 IF B$="-" THEN 2050
2180 IF B$="?" THEN HLP=26:GOSUB 3560:GOTO 2130
2190 IF LEFT$(B$,1)="Y" THEN LOGALL=-1:GOTO 2210
2200 LOGALL=0
2210 PRINT:PRINT:GOSUB 4250
2220 ' 
2230 A$="Display unvalidated users? (Y/N) ":N=1:GOSUB 2760
2240 C=1:GOSUB 2890
2250 IF B$="-" THEN 2130
2260 IF B$="?" THEN HLP=27:GOSUB 3560:GOTO 2210
2270 IF LEFT$(B$,1)="Y" THEN SHOALL=-1:GOTO 2290
2280 SHOALL=0
2290 PRINT:PRINT:GOSUB 4250
2292 ' 
2295 A1$="Set RBBS in secure mode? (Y/N) ":N=1:GOSUB 2760
2298 C=1:GOSUB 2890
2300 IF B$="-" THEN 2210
2305 IF B$="?" THEN HLP=28:GOSUB 3560:GOTO 2290
2310 IF LEFT$(B$,1)="Y" THEN SEC=-1:GOTO 2320
2315 SEC=0
2320 PRINT:PRINT:GOSUB 4250
2325 '
2330 A1$="Name of .COM file/program to run on CP/M entry  :":N=1:GOSUB 2760
2332 C=1:GOSUB 2890
2335 IF B$="-" THEN 2290
2336 IF B$="?" THEN HLP=29:GOSUB 3560:GOTO 2320
2338 EPRG$=B$
2340 PRINT:PRINT:GOSUB 4250
2342 '
2345 A1$="Name of file to Read with 'D' command  :":N=1:GOSUB 2760
2350 C=1:GOSUB 2890
2355 IF B$="-" THEN 2320
2360 IF B$="?" THEN HLP=30:GOSUB 3560:GOTO 2340
2370 DFIL$=B$
2380 PRINT:PRINT:GOSUB 4250
2385 '
2390 A1$="Ready to create the password files? (Y/N) ":N=1:GOSUB 2760
2400 C=1:GOSUB 2890
2410 IF B$="-" THEN 2340
2420 IF B$="?" THEN HLP=31:GOSUB 3560:GOTO 2380
2430 IF LEFT$(B$,1)="N" THEN 220
2440 PRINT:PRINT
2450 ' 
2460 ' Now we can create the two password files for the system
2470 ' The BOOTPWD on the current drive A: and PWDS on the drive
2480 ' taged as being DSK2$
2490 ' 
2500 PRINT "The file BOOTPWD will be written to drive A: "
2510 PRINT "and the PWDS file will be written to drive ";DSK2$
2520 PRINT
2530 A1$="Is this correct before the files are written? (Y/N) "
2540 GOSUB 3340:IF NOT OK THEN 230
2550 ' 
2560 PRINT:PRINT
2570 ' 
2580 S$=","
2590 DSK$="A:"
2600 ' 
2610 OPEN "O",1,DSK$+"BOOTPWD"
2620 PRINT #1,DSK2$;S$;DSK3$;S$;DSK4$;S$;DSK5$;S$SYS1$;S$;SYS2$;S$;VERS1$
2630 PRINT #1,TWIT;S$;DATIM;S$;SEC;S$;SHOLOC;S$;DFIL$;S$;EPRG$;S$;WHEEL
2640 CLOSE
2650 OPEN "O",1,DSK2$+"pwds"
2660 PRINT #1,P1$;S$;P2$;S$;P3$;S$;PC$;S$;VAP$;S$;EXIT$;S$;LOGALL;S$;SHOALL
2670 PRINT #1,BEEP;S$;SIZE;S$;MSYS;S$;NNUM;S$;HNUM;S$;SKIP;S$;LMSG;S$;GOCPM
2680 CLOSE
2690 PRINT:PRINT "Your password files (BOOTPWD) and (pwds) have been created."
2700 PRINT:PRINT
2710 SYSTEM
2720 END
2730 ' 
2740 ' Print string from A$ on console
2750 ' 
2760 IF SAV$<>"" AND A1$<>"" THEN A1$="":RETURN
2770 IF A1$<>"" THEN A$=A1$:A1$=""
2780 IF N=1 THEN PRINT A$;:PP$=A$:GOTO 2830
2790 BI=ASC(INKEY$+" ")
2800 IF BI=&H13 OR BI=&H53 OR BI=&H73 THEN BI=ASC(INPUT$(1)):GOTO 2820
2810 IF BI=&HB OR BI=&H4B OR BI=&H6B THEN BK=-1:GOTO 2840 
2820 PRINT A$
2830 A=A+LEN(A$)
2840 A$="":N=0
2850 RETURN
2860 ' 
2870 ' Accept string into B$ from console
2880 ' 
2890 IF BEL AND SAV$="" THEN PRINT CHR$(7);
2900 B$="":BK=0
2910 IF SAV$="" THEN GOSUB 3030:IF C<>3 THEN PRINT
2920 SP=INSTR(SAV$,";"):IF SP=0 THEN B$=SAV$:SAV$="":GOTO 2940
2930 B$=LEFT$(SAV$,SP-1):SAV$=MID$(SAV$,SP+1)
2940 IF LEN(B$)=0 THEN C=0:RETURN
2950 IF C=0 THEN 2970
2960 CY$=B$:GOSUB 3420:B$=CY$
2970 D=D+LEN(B$):C=0
2980 IF B$="+" THEN 3800
2990 RETURN
3000 ' 
3010 ' Character-at-a-time line input with editing (IF C=2, NO ECHO)
3020 ' 
3030 CHC=0: SAV$="":DC=0:IC=&H30
3040 NCH=ASC(INPUT$(1))
3050 IF NCH=13 THEN RETURN                                              ' CR
3060 IF NCH=127 THEN 3140
3070 IF NCH<32 THEN 3160
3080 IF CHC>=63 THEN PRINT CHR$(7);:GOTO 3040
3090 SAV$=SAV$+CHR$(NCH): CHC=CHC+1 :IC=IC+1:IF IC=&H3A THEN IC=&H30
3100 IF DC THEN PRINT CHR$(10);
3110 IF C<>2 THEN PRINT CHR$(NCH); ELSE PRINT CHR$(IC);
3120 IF CHC=55 THEN PRINT CHR$(7);
3130 DC=0:GOTO 3040
3140 IF CHC=0 THEN 3040 ELSE PRINT BSL$;:DC=-1
3150 CHC=CHC-1:IC=IC-1: SAV$=LEFT$(SAV$,CHC): GOTO 3040
3160 IF CHC=0 THEN 3040
3170 IF NCH=8 THEN PRINT ERS$;:DC=0:GOTO 3150                           ' BS
3180 IF NCH=12 THEN GOSUB 3240:GOTO 3250                                ' ^L
3190 IF NCH=18 THEN PRINT:PRINT PP$;:GOTO 3250                          ' ^Retype
3200 IF NCH=21 THEN PRINT " #": PRINT PP$;:DC=0:GOTO 3030               ' ^U
3210 IF NCH<>24 THEN 3040                                               ' ^X
3220 GOSUB 3240
3230 GOTO 3030
3240 FOR BCC=1 TO CHC: PRINT ERS$;: NEXT BCC: RETURN
3250 IF C<>2 THEN PRINT SAV$;: GOTO 3300
3260 ' 
3270 ' Print numbers to hide password
3280 ' 
3290 IC=&H30:FOR BCC=1 TO CHC: IC=IC+1: PRINT CHR$(IC);: NEXT BCC
3300 DC=0:GOTO 3040
3310 ' 
3320 ' Prompt for YES or NO answer
3330 ' 
3340 A2$=A1$:OK=0
3350 A1$=A2$:N=1:GOSUB 2760:C=1:GOSUB 2890:ANS$=LEFT$(B$,1)
3360 IF ANS$="" THEN 3380 ELSE IF ANS$="Y" THEN OK=-1:RETURN
3370 IF ANS$="N" THEN RETURN
3380 A$="<Y or N>":GOSUB 2760:GOTO 3340
3390 ' 
3400 ' Uppercase string CY$ (frank -> FRANK)
3410 ' 
3420 FOR ZZ=1 TO LEN(CY$)
3430 ZA=ASC(MID$(CY$,ZZ,1)):IF ZA<&H61 OR ZA>&H7A THEN 3450
3440 MID$(CY$,ZZ,1)=CHR$(ZA-&H20)
3450 NEXT ZZ
3460 RETURN
3470 ' 
3480 INPUT "Press RETURN to continue.",A$
3490 FOR SCROLL=1 TO 24:PRINT:NEXT SCROLL:RETURN
3500 ' 
3510 IF LEN(B$)>SL THEN RTN=0:RETURN
3520 RTN=1:RETURN
3530 ' 
3540 ' Come here to find and print the help files
3550 ' 
3560 CHK$=STR$(HLP)+"*"
3570 ND$=" **"
3580 TXT=0
3590 OPEN "I",1,DSK$+"INSTALL.HLP":BK=0:IF EOF(1) THEN 3660
3600 IF EOF(1) OR BK THEN 3660 ELSE LINE INPUT #1,A$
3610 A$=" "+A$
3620 IF A$=CHK$ THEN TXT=1:A$=" ":A$=" ":GOTO 3600
3630 IF A$=ND$ AND TXT=1 THEN 3660
3640 IF TXT=1 THEN GOSUB 2760
3650 GOTO 3600
3660 CLOSE #1:RETURN
3670 PRINT:PRINT:NF1=0:NF2=0
3680 ' 
3690 ' Come here to get the existing password file data
3700 ' 
3710 ON ERROR GOTO 4300
3720 OPEN "I",1,DSK$+"BOOTPWD":IF EOF(1) THEN 3740
3730 INPUT #1,DSK2$,DSK3$,DSK4$,DSK5$,SYS1$,SYS2$,VERS1$,TWIT
3735 INPUT #1,DATIM,SEC,SHOLOC,DFIL$,EPRG$,WHEEL
3740 CLOSE #1
3750 OPEN "I",1,DSK2$+"pwds":IF EOF(1) THEN 3780
3760 INPUT #1,P1$,P2$,P3$,PC$,VAP$,EXIT$,LOGALL,SHOALL
3770 INPUT #1,BEEP,SIZE,MSYS,NNUM,HNUM,SKIP,LMSG,GOCPM
3780 CLOSE #1
3790 ' 
3800 PRINT
3810 PRINT "1.   DSK2$ -> ";DSK2$,"2.  DSK3$ -> ";DSK3$
3820 PRINT "3.   FIRST Name -> ";SYS1$,"4.  LAST Name -> ";SYS2$
3830 PRINT "5.   RBBS name -> ";VERS1$
3840 PRINT "6.   Direct CP/M password -> ";P1$,"7.  SYSOP password -> ";P2$
3845 IF WHEEL THEN A$="YES" ELSE A$="NO"
3850 PRINT "8.   Set Wheel for SYSOP -> ";A$,"9. Validation password -> ";VAP$
3860 PRINT "10.  CP/M entry question -> ";PC$
3870 PRINT "11.  Answer to CP/M entry question -> ";P3$
3880 IF MSYS THEN A$="YES" ELSE A$="NO"
3890 PRINT "12.  Multiple SYSOPs -> ";A$
3900 PRINT "13.  CHAT delay value -> ";BEEP,"14.  Message length -> ";SIZE
3910 PRINT "15.  Number NEWS messages -> ";NNUM,"16.  NEWS drive ";DSK5$
3920 PRINT "17.  Number HELP messages -> ";HNUM,"18.  HELP drive ";DSK4$
3930 IF SKIP THEN A$="YES" ELSE A$="NO"
3940 PRINT "19.  Skip (Highest Message Read) -> ";A$
3950 IF TWIT THEN A$="YES" ELSE A$="NO"
3960 PRINT "20.  Logout TWITs -> ";A$
3970 IF LMSG=1 THEN A$="Anyone but TWITs"
3980 IF LMSG=2 THEN A$="VALID and SUPER users"
3990 IF LMSG=3 THEN A$="SUPER users only"
4000 PRINT "21.  Who can enter messages -> ";A$
4010 IF GOCPM=1 THEN A$="Anyone but TWITs"
4020 IF GOCPM=2 THEN A$="VALID and SUPER users"
4030 IF GOCPM=3 THEN A$="SUPER users only"
4040 PRINT "22.  Who can go to CP/M -> ";A$
4050 IF DATIM THEN A$="YES" ELSE A$="NO"
4060 PRINT "23.  External clock -> ";A$
4070 PRINT "24.  Goodbye program -> ";EXIT$
4080 IF SHOLOC THEN A$="YES" ELSE A$="NO"
4090 PRINT "25.  Show location of callers and users -> ";A$
4100 IF LOGALL THEN A$="YES" ELSE A$="NO"
4110 PRINT "26.  Log unvalidated callers -> ";A$
4120 IF SHOALL THEN A$="YES" ELSE A$="NO"
4130 PRINT "27.  Show unvalidated users -> ";A$;,
4140 IF SEC THEN A$="YES" ELSE A$="NO"
4145 PRINT "28.  SECURE Mode -> ";A$
4150 PRINT "29.  Exit file to run -> ";EPRG$,"30.  D File to print -> ";DFIL$
4160 PRINT "31.  DONE -> create new password files."
4170 PRINT
4180 ' 
4190 INPUT "Enter the number of your choice: ",ENTRY
4200 IF ENTRY >16 THEN ENTRY=ENTRY-16:GOTO 4220
4210 ON ENTRY GOTO 260,330,400,470,540,610,680,710,750,820,890,960,1040,1110,1180,1250
4220 ON ENTRY GOTO 1320,1390,1460,1540,1620,1760,1900,1980,2050,2130,2210,2290,2320,2340,2380
4230 GOTO 230
4240 ' 
4250 PRINT " <?> For HELP "
4260 PRINT " <-> For previous MENU "
4270 PRINT " <+> To return to Select Menu "
4280 PRINT:RETURN 
4290 ' 
4300 PRINT:PRINT "Old password file not found, new password file initialized."
4310 CLOSE #1
4320 PRINT:PRINT:GOTO 230
