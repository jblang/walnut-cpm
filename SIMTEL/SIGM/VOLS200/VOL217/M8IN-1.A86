; M8IN-1.A86 -- Overlay file for the CompuPro I3/I4.  09/14/84
;
; This overlay adapts the MDM8xx program to the CompuPro Interfacer 3 or
; Interfacer 4 serial cards using the 2651 USART chip.
;
; You will want to look this file over carefully. There are a number of
; options that you can use to configure the program to suit your taste.
; This file places particular emphasis on using an external modem that
; is connected to a serial port.  This includes acoustic modems as well
; as "intelligent" modems such as the Hayes Smartmodem, Racal-Vadic, Bell
; Dataphone 212A, etc.
;
; This overlay is capable of setting the baud rate, disconnecting the
; modem, and sending breaks.
;
;
;	TO USE: First edit this file filling in answers for your own
;		equipment.  Then assemble with ASM86.CMD or equivalent
;		assembler.  Then use M8CNFG to overlay the the results
;		of this program to the original MDM8xx.H86 file:
;
;			ASM86 M8IN-1
;			REN M8OVL.H86=M8IN-1.H86
;			M8CNFG
;			GENCMD MDM 8080 CODE[MFF0]
;
;		 Now run MDM.  Have Fun
;
;
; =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =   =
;
; 09/19/84 - Original version for MDM8xx	- Alex Soya
; 04/04/83 - First version of this file		- Irv Hoff
;
; =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =
;
BELL	EQU	07H			;bell
CR	EQU	0DH			;carriage return
ESC	EQU	1BH			;escape
LF	EQU	0AH			;linefeed
;
YES	EQU	0FFH
NO	EQU	0
;
; Change the following to match your equipment
;
; =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =   =	  =
;
BASE	EQU	010H			;base port of CompuPro I3 or I4 cards
UPORT	EQU	BASE+7			;user (chip select) port
MDM	EQU	6			;chip that controls modem port
;
;
;
DPORT	EQU	BASE			;data port
SPORT	EQU	BASE+1			;status port
MPORT	EQU	BASE+2			;mode port
CPORT	EQU	BASE+3			;control port
;
TBMT	EQU	01H			;transmit buffer empty
DAV	EQU	02H			;data available
;
	ORG	100H
;
; Change the clock speed to suit your system
;
	RS	3			;(for  "JMP   START" instruction)
;
PMMIMODEM	DB	NO		;yes=PMMI S-100 Modem			103H
SMARTMODEM	DB	YES		;yes=HAYES Smartmodem, no=non-PMMI	104H
TOUCHPULSE	DB	'T'		;T=touch, P=pulse (Smartmodem-only)	105H
CLOCK		DB	80		;clock speed in MHz x10, 25.5 MHz max.	106H
					;20=2 MHh, 37=3.68 MHz, 40=4 MHz, etc.
MSPEED		DB	1		;0=110 1=300 2=450 3=600 4=710 5=1200	107H
					;6=2400 7=4800 8=9600 9=19200 default
BYTDLY		DB	5		;0=0 delay  1=10ms  5=50 ms - 9=90 ms	108H
					;default time to send character in ter-
					;minal mode file transfer for slow BBS.
CRDLY		DB	5		;0=0 delay 1=100 ms 5=500 ms - 9=900 ms 109H
					;default time for extra wait after CRLF
					;in terminal mode file transfer
NOOFCOL		DB	5		;number of DIR columns shown		10AH
SETUPTST	DB	YES		;yes=user-added Setup routine		10BH
SCRNTEST	DB	YES		;Cursor control routine			10CH
ACKNAK		DB	YES		;yes=resend a record after any non-ACK	10DH
					;no=resend a record after a valid-NAK
BAKUPBYTE	DB	NO		;yes=change any file same name to .BAK	10EH
CRCDFLT		DB	YES		;yes=default to CRC checking		10FH
TOGGLECRC	DB	YES		;yes=allow toggling of CRC to Checksum	110H
CONVBKSP	DB	NO		;yes=convert backspace to rub		111H
TOGGLEBK	DB	YES		;yes=allow toggling of bksp to rub	112H
ADDLF		DB	NO		;no=no LF after CR to send file in	113H
					;terminal mode (added by remote echo)
TOGGLELF	DB	YES		;yes=allow toggling of LF after CR	114H
TRANLOGON	DB	YES		;yes=allow transmission of logon	115H
					;write logon sequence at location LOGON
RESERVED	DB	NO		; Resevered for future release		116H
LOCONEXTCHR	DB	NO		;yes=local command if EXTCHR precedes	117H
					;no=external command if EXTCHR precedes
TOGGLELOC	DB	YES		;yes=allow toggling of LOCONEXTCHR	118H
LSTTST		DB	YES		;yes=printer available on printer port	119H
XOFFTST		DB	NO		;yes=checks for XOFF from remote while	11AH
					;sending a file in terminal mode
XONWAIT		DB	NO		;yes=wait for XON after CR while	11BH
					;sending a file in terminal mode
TOGXOFF		DB	YES		;yes=allow toggling of XOFF checking	11CH
IGNORCTL	DB	NO		;yes=CTL-chars above ^M not displayed	11DH
EXTRA1		DB	0		;for future expansion			11EH
EXITCHR		DB	'E'-40H		;^E = Exit to main menu			11FH
BRKCHR		DB	'@'-40H		;^@ = Send 300 ms. break tone		120H
NOCONNCT	DB	'N'-40H		;^N = Disconnect from the phone line	121H
LOGCHR		DB	'L'-40H		;^L = Send logon			122H
LSTCHR		DB	'P'-40H		;^P = Toggle printer			123H
UNSAVE		DB	'R'-40H		;^R = Close input text buffer		124H
TRANCHR		DB	'T'-40H		;^T = Transmit file to remote		125H
SAVECHR		DB	'Y'-40H		;^Y = Open input text buffer		126H
EXTCHR		DB	'^'-40H		;^^ = Send next character		127H
;
;
	RS	3			;				128H
;
IN@MODCTL1:
	JMP	IMCTL1			;in modem control port		12BH
	DB	0,0,0,0,0,0,0		; spares
;
OUT@MODDATP:
	JMP	OMDP			;out modem data port		135H
	DB	0,0,0,0,0,0,0		; spares
;
IN@MODDATP:
	JMP	IMDP			;in modem data port		13FH
	DB	0,0,0,0,0,0,0		; spares
;
ANI@MODRCVB:	AND AL,DAV		;bit to test for receive ready	149H
		RET
;
CPI@MODRCVR:	CMP AL,DAV		;value of rcv. bit when ready	14CH
		RET
;
ANI@MODSNDB:	AND AL,TBMT		;bit to test for send ready	14FH
		RET
;
CPI@MODSNDR:	CMP AL,TBMT		;value of send bit when ready	152H
		RET
;
	RS	6			;				156H
;
OUT@MODCTL1:	RET 
	RS	2			;out modem control port #2	15BH
;
OUT@MODCTL2:	RET 
	RS	2			;out modem control port #1	15EH
;
LOGONPTR DW	(Offset LOGON)		;for user message.		161H
	RS	6			;				163H
JMP@GOODBYE:JMP GOODBYE			;				169H
JMP@INITMOD:JMP INITMOD			;go to user written routine	16CH
	RET
	NOP				;(by-passes PMMI routine)	170H
	NOP
	RET
	NOP				;(by-passes PMMI routine)	173H
	NOP
	RET
	NOP				;(by-passes PMMI routine)	176H
	NOP
JMP@SETUPR:    JMP  SETUPR		;				178H
JMP@SPCLMENU:  JMP  SPCLMENU		;				17BH
JMP@SYSVER:    JMPS SYSVER		;make sure this is SHORT Jump	17EH
JMP@BREAK:     JMP  SENDBRK		;				180H

;
; Do not change the following eight lines.
;
JMP@ILPRT:
		RS	3		;				183H
JMP@INBUF:
		RS	3		;				186H
JMP@INLNCOMP:
		RS	3		;				189H
JMP@INMODEM:
		RS	3		;				18CH
JMP@NXTSCRN:
		RS	3		;				18FH
JMP@TIMER:
		RS	3		;				192H
JMP@CTYPE:
		RS	3		;				195H
JMP@KEYIN:
		RS	3		;				198H
;
;
; Routine to clear to end of screen.  If using CLREOS and CLRSCRN, set
; SCRNTEST to YES at 010AH (above).
;
CLREOS: CALL	JMP@ILPRT		;				19BH
	DB	ESC,'Y',0,0,0		;				19EH
	RET				;				1A3H
;
CLRSCRN:CALL	JMP@ILPRT		;				1A4H
	DB	ESC,'*',0,0,0		;				1A7H
	RET				;				1ACH
;
SYSVER: CALL	JMP@ILPRT		;				1ADH
	DB	'Version for CompuPro '
	DB	'Interfacer-3 or 4 (port ',MDM+'0',')'
	DB	CR,LF,0
	RET
;.....
;
;
;-----------------------------------------------------------------------
;
; NOTE:	 You can change the SYSVER message to be longer or shorter.  The
;	 end of your last routine should terminate by 0600H if using the
;	 Hayes Smartmodem or by address 0C00H otherwise.
;
;-----------------------------------------------------------------------
;
; You can put in a message at this location which can be called up with
; CTL-O if TRANLOGON has been set TRUE.	 You can use several lines if
; desired.  End with a 0.
;
LOGON	DB	'It has been a long day, so now it'
	DB	' is time for a beer!',CR,LF,0
;
;.....
;
;
;
;
; Add your own routine here to read a byte from your Modem Control port
;
IMCTL1: MOV	AL,MDM		; Select Interfacer 3/4 Port
	OUT	UPORT,AL
	IN	AL,SPORT
	RET
;
;....
;
;
; Add your own routine here to send a byte in AL to Modem Data Port
;
OMDP:	PUSH	AX		; Save Data while Selecting Port
	MOV	AL,MDM
	OUT	UPORT,AL
	POP	AX
	OUT	DPORT,AL
	RET
;
;
;....
;
; Add your own routine here to read Modem Data Port into AL
;
IMDP:	MOV	AL,MDM		; Select Uart first
	OUT	UPORT,AL
	IN	AL,DPORT	; Now get Data
	RET


; Add your own routine here to send a break tone to reset some time-
; share computers, if desired.
;
SENDBRK:MOV	AL,MDM			;select chip
	OUT	UPORT,AL
	MOV	AL,2FH			;send break for 300ms
	JMPS	GOODBYE1
;.....
;
;
; Add your own routine here to put DTR low and/or send a break tone.
; Check other routines such as MDM711DP.ASM which is using this feature.
;
GOODBYE:MOV	AL,MDM			;select chip
	OUT	UPORT,AL
	XOR	AL,AL			;turn off DTR
;
GOODBYE1:
	OUT	CPORT,AL
	MOV	CH,3			;turn off DTR for 300 ms.
	CALL	JMP@TIMER
	MOV	AL,27H			;turn DTR back on
	OUT	CPORT,AL
	RET
;.....
;
;
; You can use this area for any special initialization or setup you may
; wish to include.  Each must stop with a RET.	You can check the other
; available overlays for ideas how to write your own routines if that
; may be of some help.
;
INITMOD:MOV	AL,MDM			;select chip
	OUT	UPORT,AL
;
INITMOD1:
	MOV	AL,4EH			;select 8 data bits, no parity, 1 stop
	OUT	MPORT,AL
;
INITMOD2:
	MOV	BL,Byte Ptr MSPEED	;Get Baud rate
	DEC	BL			; adjust for table offset
	XOR	BH,BH			; make 16 bit
	MOV	AL, Byte Ptr BDTBL[BX]
	OUT	MPORT,AL
	MOV	AL,27H			;turn on DTR etc.
	OUT	CPORT,AL
	RET
;.....
;
;
; Use the 'SET' command to select a desired baud rate
;
SETUPR: MOV	DX,(Offset BAUDBUF)	;point to new input buffer
	CALL	JMP@ILPRT
	DB	'Input Baud Rate (300, 600, 1200, 4800, 9600, 19200): ',0
	CALL	JMP@INBUF
	MOV	DX,(Offset BAUDBUF)+2
	CALL	JMP@INLNCOMP		;compare BAUDBUF+2 with characters below
	DB	'300',0
	JNB	OK300			;go if got match
	CALL	JMP@INLNCOMP
	DB	'600',0
	JNB	OK600
	CALL	JMP@INLNCOMP
	DB	'1200',0
	JNB	OK1200
	CALL	JMP@INLNCOMP
	DB	'4800',0
	JNB	OK4800
	CALL	JMP@INLNCOMP
	DB	'9600',0
	JNB	OK9600
	CALL	JMP@INLNCOMP
	DB	'19200',0
	JNB	OK19200
	CALL	JMP@ILPRT		;all matches failed, tell operator
	DB	'++ Incorrect entry ++',CR,LF,BELL,0
	JMP	SETUPR			;try again
;
OK300:	MOV	AL,1			;MSPEED 300 baud value
	JMPS	LOADBD			;go load everything
;
OK600:	MOV	AL,3
	JMPS	LOADBD
;
OK1200: MOV	AL,5
	JMPS	LOADBD
;
OK4800: MOV	AL,7
	JMPS	LOADBD
;
OK9600: MOV	AL,8
	JMPS	LOADBD
;
OK19200:MOV	AL,9
;
LOADBD: MOV	Byte Ptr MSPEED,AL	;chnage time-to-send to match baudrate
	JMP	INITMOD			;reinitialize to new baudrate, then done
;...
;
BDRTE	DB	0		; keeps selected uart Baud Rate Value

; TABLE OF BAUDRATE PARAMETERS
;
BD300	EQU	35H			;300  baud
BD450	EQU	0			;450  baud NOT SUPPORTED
BD600	EQU	36H			;600  baud
BD710	EQU	0			;710  baud NOT SUPPORTED
BD1200	EQU	37H			;1200 baud
BD2400	EQU	0			;2400 baud NOT SUPPORTED
BD4800	EQU	3CH			;4800 baud
BD9600	EQU	3EH			;9600 baud
BD19200 EQU	3FH			;19200 baud
;
BAUDBUF DB	10,0
	RS	10

BDTBL	DB BD300,BD450,BD600,BD710,BD1200,BD2400,BD4800,BD9600,BD19200
;
;	       (END OF INITMOD AND SETUP ROUTINES)
;=======================================================================
;
;
; If using the Hayes Smartmodem this is unavailable without a special
; change.
;
SPCLMENU:RET
;
;
; NOTE:	 MUST TERMINATE PRIOR TO 0600H (with Smartmodem)
;				 0C00H (without Smartmodem)
;.....
;
	END
