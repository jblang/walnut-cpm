;
; VLIB Module Name:  VID2
; Author:  Richard Conn
; VLIB  Version Number:  1.0
; Module Version Number:  1.0
; Module Entry Points:
;	EREOL
; Module External References:
;	VIDPTR	VIDOUT	VIDSKP
;
	ext	vidptr,vidout,vidskp
;
; Erase to End of Line
;	Return with A=0 and Zero Flag Set if not done
;
ereol::
	push	h	;save regs
	push	d
	lhld	vidptr	;pt to environment
	mov	a,m	;no terminal?
	cpi	' '+1
	jc	err
	lxi	d,16h	;pt to ereol delay
	dad	d
	mov	d,m	;get it
	inx	h	;pt to cls string
	call	vidskp	;skip over it
	call	vidskp	;skip over CM string
	mov	a,m	;get first char of ereol string
	ora	a	;if no string, error
	jz	err
	call	vidout	;output string with delay
	pop	d	;done
	pop	h
	xra	a	;return NZ
	dcr	a
	ret
err:
	pop	d	;done
	pop	h
	xra	a	;return Z
	ret

	end
